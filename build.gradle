plugins {
    id 'java-library'
    id 'maven-publish'
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    version = project.api_version
    group = project.maven_group

    configurations {
        library
    }

    repositories {
        mavenCentral()
        maven {
            name "Quantum"
            url "https://maven.quantumclient.org/snapshots"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
                artifact sourcesJar

            }
        }

        repositories {
            maven {
                url "https://maven.quantumclient.org/snapshots"
                credentials {
                    username = System.getProperty('MAVEN_USERNAME')
                    password = System.getProperty('MAVEN_PASSWORD')
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    jar {
        from {
            configurations.library.collect {
                it.isDirectory() ? it :  zipTree(it)
            }
        }
    }

}

task buildAll(group: 'build') {
    subprojects.forEach(p -> buildAll.dependsOn(p.tasks.build))
}

task publishAll(group: 'publishing') {
    subprojects.forEach(p -> publishAll.dependsOn(p.tasks.publish))
}
